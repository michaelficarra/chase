program
	: /* nothing */
	| formulaList optNEWLINE

formulaList
	: formula
	| formulaList NEWLINE formula

formula
	: expr
	| expr IMPLIES expr
	| FOR_ALL argList quantifierBody
	| THERE_EXISTS argList quantifierBody

quantifierBody: optCOLON formula

expr: exprOR

exprOR
	: exprAND
	| exprOR OR exprAND

exprAND
	: exprValue
	| exprAND AND exprValue

exprValue
	: atomic
	| PAREN_OPEN formula PAREN_CLOSE
	| BRACKET_OPEN formula BRACKET_CLOSE
	| TAUTOLOGY
	| CONTRADICTION
	| NOT exprValue

atomic: PREDICATE index

index
	: PAREN_OPEN argList PAREN_CLOSE
	| BRACKET_OPEN argList BRACKET_CLOSE

argList
	: arg
	| argList COMMA arg

arg: FREE_VARIABLE

optCOLON:   /* nothing */ | COLON
optNEWLINE: /* nothing */ | NEWLINE
